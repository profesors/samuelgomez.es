== Variable compleja

=== Introducción

Las variables complejas son de primera clase en Fortran. Tan solo será necesario declarar la
variable de ese tipo y podemos acceder a su parte real e imaginaria con `z%re` y con `z%im` pero
también se puede asignar una valor complejo en un solo paso con `z=cmplx(0,1)`

[source,fortran]
--
program coseno
    complex w, z


    ! Entrada
    write (*, "(a)", advance="no") "Valor real de z: "
    read *, z%re
    write (*, "(a)", advance="no") "Valor imaginario de z: "
    read *, z%im

	! También se puede usar la función intrínseca
	! z = cmplx(a, b)

    ! Cálculos
    w = cos(z)

    ! Salida
    write (*,"(a)") "w=cos(z)"
    write (*,"(a,f4.2,sp,f5.2,a)") "z:", real(z), aimag(z), "i"
    write (*,"(a,f4.2,sp,f5.2,a)") "w:", real(w), aimag(w), "i"

end program coseno
--


=== Integrales complejas

Una integral compleja puede ser calculada mediante una serie sucesiva de sumas. Las iteraciones
deben ser parametrizadas. En este ejemplo, el parámetro de iteración paso a paso es *t*. Cada
iteración es la suma del producto formado por la función en el punto en función de *f(gamma(t))* por la
derivada del camino *gamma'(t)* por el diferencial *dt*.


[source,fortran]
--
program Integral_compleja
! Calcular $\int_{\gamma=\{|z|=1\}} \frac{Ln z}{z} dz$
! SOLUCIÓN
! Para calular esta integral compleja debemos hallar el camino y la derivada de este a lo largo del
! contorno
!
! Camino: 					gamma(t)=e^{it}
! Derivada del camino:		gamma'(t)=ie^{it} dt
! Función:					f(gamma(t))=\frac{Ln e^{it}}{e^{it}}=\frac{it}{e^{it}}

    implicit none
    real, parameter:: pi = 3.141592654
    real, parameter:: t_0=0, t_end=2*pi
    real, parameter:: dt = 0.001
    real:: t
    complex:: s                 ! Suma
    complex:: i = cmplx(0,1)

    ! Inicializar variables
    t = t_0
    s = cmplx(0,0)

    ! Bucle de la integral
    write (*, "(a25)") "------------------------"
    write (*,"(a8,a8,a9)") "Iter.", "Re", "Im"
    write (*, "(a25)") "------------------------"
    do while (t <= t_end)
        s = s + f(t) * fp(t) * dt
        write (*,"(f8.2,f8.2, sp, f8.2, a1)") t, s%re, s%im, "i"
        t = t + dt
    end do

    ! Mostrar resultados
    write (*, "(a25)") "------------------------"




contains

    complex function f(t) result (res)
        ! Función a integrar: f(gamma(t))
        implicit none
        real, intent(in):: t
        res = (i*t)/(exp(i*t))
    end function f


    complex function fp(t) result(res)
        ! Derivada del camino en función de t. Es decir: gamma'(t)
        implicit none
        real, intent(in):: t
        res = i * exp(i*t)
    end function fp

end program Integral_compleja
--
